#
# Opens and manages a SSH tunnel.
# Author: Brad Frank
# Date: Feb 2022
# Tested: zsh 5.8 (x86_64-apple-darwin19.6.0)
#

ssht() {
  local usage remote_host proxy_host port
  zparseopts -E -D -- \
    h=usage -help=usage \
    r:=remote_host -remote:=remote_host \
    x:=proxy_host -proxy:=proxy_host \
    p:=port -port:=port

  _ssht_usage() {
    echo "ssht [-h|--help] | [create -r|--remote <remote> -x|--proxy <proxy> -p|--port <port>] | check | exit"
  }

  [[ -n "$usage" ]] && { _ssht_usage; return 0; }

  case "$1" in
    create) export SSH_PROXY="${proxy_host[2]}"
            ssh -M -S "$SSH_PROXY" -fNL "${port[2]}:${remote_host[2]}:${port[2]}" "$SSH_PROXY"
            ;;
    check)  ssh -S "$SSH_PROXY" -O check "$SSH_PROXY" ;;
    exit)   ssh -S "$SSH_PROXY" -O exit "$SSH_PROXY"; unset SSH_PROXY ;;
    *)      _ssht_usage >&2; return 1 ;;
  esac
}

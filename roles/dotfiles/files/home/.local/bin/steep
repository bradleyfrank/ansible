#!/usr/bin/env zsh

set -o errexit

# @meta describe Runs updates on various programs and plugins.
# @meta author Brad Frank
# @meta require-tools jq,toilet
# @meta version 2.1.0
# @meta combine-shorts


_hr() {
  printf '\n\n%s%s' "$(tput bold)" "$(tput setaf 6)"
  printf '%s\n' "$(toilet -f smmono9 "$1")"
  printf '%s\n' "$(tput sgr0)"
}

pkg_installed() { grep --quiet "$1" "$INSTALLED_PACKAGES"; }


macos_update() {
  _hr "SYSTEM UPDATE"
  sudo softwareupdate --list | grep --quiet 'Action: restart' && RESTART_NEEDED=1
  sudo softwareupdate --install --all --agree-to-license
  homebrew_update
}


linux_update() {
  _hr "SYSTEM UPDATE"

  sudo systemctl stop packagekit
  # shellcheck disable=SC1091
  source /etc/os-release

  case "$ID" in
    fedora)
      sudo dnf clean all
      sudo dnf makecache
      sudo dnf upgrade --assumeyes
      # shellcheck disable=SC2024
      sudo dnf list --installed >> "$INSTALLED_PACKAGES"
      if ! sudo dnf needs-restarting -r; then RESTART_NEEDED=1; fi
      ;;
    debian|ubuntu|linuxmint)
      sudo apt-get clean
      sudo apt-get update --yes
      sudo DEBIAN_FRONTEND=noninteractive apt-get upgrade --yes
      sudo dpkg --list | grep --extended-regexp "^ii" | awk '{print $2}' >> "$INSTALLED_PACKAGES"
      if [[ -e /var/run/reboot-required ]]; then RESTART_NEEDED=1; fi
      ;;
  esac

  if pkg_installed flatpak; then
    _hr "FLATPAK UPDATE"
    flatpak update
  fi

  if pkg_installed fwupd; then
    _hr "FIRMWARE UPDATE"
    sudo fwupdmgr refresh
    sudo fwupdmgr update
  fi

  if [[ -n $HOMEBREW_PREFIX ]]; then
    homebrew_update
  fi
}


homebrew_update() {
  _hr "HOMEBREW UPDATE"
  brew update
  brew upgrade
  brew autoremove
  brew cleanup
  brew list --cask -1 >> "$INSTALLED_PACKAGES"
  brew list --formula -1 >> "$INSTALLED_PACKAGES"
}


INSTALLED_PACKAGES="$(mktemp)"

case "$(uname -s)" in
  Darwin) macos_update ;;
   Linux) linux_update ;;
esac

if pkg_installed google-cloud-sdk; then
  _hr "GCLOUD UPDATE"
  gcloud components update --quiet
fi

if pkg_installed krew; then
  _hr "KREW UPDATE"
  kubectl krew update
  kubectl krew upgrade
fi

if [[ -f ~/.config/zsh/rc.d/benv.zsh ]]; then
  _hr "CLEANUP: VIRTUALENV"
  # shellcheck disable=SC1090
  source ~/.config/zsh/rc.d/benv.zsh
  benv list
  benv cleanup
fi

if pkg_installed multipass; then
  _hr "MULTIPASS PURGE"
  deleted_vms="$(multipass list --format json \
    | jq -r '.list[] | select(.state == "Deleted") | .name')"
  if (( "$(wc -l <<< "$deleted_vms)")" > 0 )); then
    echo "$deleted_vms"
    multipass purge
  fi
fi

if pkg_installed bat; then
  _hr "CACHE BAT THEMES"
  bat cache --clear && bat cache --build
fi

if pkg_installed atuin; then
  _hr "SYNC HISTORY"
  atuin sync
fi

rm --recursive --force "$INSTALLED_PACKAGES"

if [[ -n $RESTART_NEEDED ]]; then
  printf '\n\n%s%s' "$(tput bold)" "$(tput setaf 1)"
  printf '%s\n' "$(toilet -f smmono9 "RESTART NEEDED")"
  printf '%s\n' "$(tput sgr0)"
fi


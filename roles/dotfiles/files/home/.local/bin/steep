#!/usr/bin/env zsh

set -o errexit

#
# Updates various package managers and components.
# Author: Brad Frank
# Date: Sept 2021; Jan 2024
# Tested: zsh 5.9 (arm-apple-darwin23.0.0)
#

pkg_installed() { grep --quiet "$1" "$INSTALLED_PACKAGES"; }


macos_update() {
  sudo softwareupdate --list | grep --quiet 'Action: restart' && RESTART_NEEDED=1
  sudo softwareupdate --install --all --agree-to-license
  homebrew_update
}


linux_update() {
  sudo systemctl stop packagekit
  source /etc/os-release
  case "$ID" in
    fedora)
      sudo dnf clean all
      sudo dnf makecache
      sudo dnf upgrade --yes
      sudo dnf list installed >> "$INSTALLED_PACKAGES"
      if ! sudo dnf needs-restarting -r; then RESTART_NEEDED=1; fi
      ;;
    debian|ubuntu|linuxmint|pop)
      sudo apt-get clean
      sudo apt-get update --yes
      sudo DEBIAN_FRONTEND=noninteractive apt-get upgrade
      sudo dpkg --list | grep --extended-regexp "^ii" | awk '{print $2}' >> "$INSTALLED_PACKAGES"
      if [[ -e /var/run/reboot-required ]]; then RESTART_NEEDED=1; fi
      ;;
  esac
  if [[ -n $HOMEBREW_PREFIX ]]; then  homebrew_update; fi
  if pkg_installed flatpak;     then  flatpak update; fi
  if pkg_installed fwupd;       then  sudo fwupdmgr refresh; sudo fwupdmgr update; fi
}


homebrew_update() {
  brew update
  brew upgrade
  brew autoremove
  brew cleanup
  brew list --cask -1 >> "$INSTALLED_PACKAGES"
  brew list --formula -1 >> "$INSTALLED_PACKAGES"
}


INSTALLED_PACKAGES="$(mktemp)"
bmux save

case "$(uname -s)" in
  Darwin) macos_update ;;
   Linux) linux_update ;;
esac

if [[ -f ~/.zshrc.d/benv.zsh ]];   then source ~/.zshrc.d/benv.zsh; benv cleanup; fi
if pkg_installed bat;              then bat cache --clear; bat cache --build; fi
if pkg_installed google-cloud-sdk; then gcloud components update --quiet; fi
if pkg_installed krew;             then kubectl krew update; kubectl krew upgrade; fi
if pkg_installed multipass;        then multipass purge; fi
if pkg_installed atuin;            then atuin sync; fi

sudo rm --recursive --force \
  "/Library/Application Support/Logitech.localized/LogiOptionsPlus/logi_ai_prompt_builder" \
  "${HOME}/ai_overlay_tmp"

if [[ -n $RESTART_NEEDED ]]; then
  toilet --font future --filter border --gay RESTART NEEDED
fi

rm --recursive --force "$INSTALLED_PACKAGES"
